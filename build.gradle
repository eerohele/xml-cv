plugins {
    id 'com.github.eerohele.saxon-gradle' version '0.1.3'
    id 'de.undercouch.download' version '3.0.0'
}

import de.undercouch.gradle.tasks.download.Download

apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    runtime 'com.thaiopensource:jing:20091111'
    runtime 'com.thaiopensource:trang:20091111'
}

ext {
  rnc = project.file('schema/cv.rnc')
  rng = project.file("$buildDir/cv.rng")
  xml = project.file('cv.xml')
  xsl = project.file('xsl/cv.xsl')
  css = project.file('css/main.css')
}

// Translate RelaxNG Compact to RelaxNG XML so that we can validate an instance
// of the schema with Exalt (https://github.com/eerohele/exalt).
task translate(type: JavaExec) {
    doFirst { ant.mkdir dir: buildDir }
    inputs.files rnc
    outputs.file rng
    classpath = configurations.runtime
    main = "com.thaiopensource.relaxng.translate.Driver"
    args rnc, rng
}

// Validate the input XML file against the RelaxNG schema.
task validate(type: JavaExec, dependsOn: translate) {
    inputs.files xml
    classpath = configurations.runtime
    main = "com.thaiopensource.relaxng.util.Driver"
    args rng, xml
}

task downloadCss(type: Download) {
    src([
        'https://cdn.jsdelivr.net/normalize/3.0.3/normalize.min.css',
        'https://cdn.jsdelivr.net/skeleton/2.0.4/css/skeleton.css'
    ])

    dest buildDir
}

// Convert the input XML file into HTML5 with the Saxon Gradle plugin
// (https://github.com/eerohele/saxon-gradle).
xslt {
    dependsOn validate, downloadCss
    inputs.files css
    input xml
    stylesheet xsl
}

task cleanUp << {
    ant.delete file: rng
    ant.delete dir: project.buildDir
}

defaultTasks 'xslt'
